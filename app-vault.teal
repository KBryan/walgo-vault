#pragma version 2

// Global Vars:
// position 2: is registered
// position 3: Vault address (no Admin operations)
// position 4: Admin address

// check if the app is being created
// if so save creator
int 0
txn ApplicationID
==
bz not_creation

/////////////////////////////////////////////////////////////////////
// BEGIN: Creation of the app
// ==========================
// Admin -> createApp: args []
//

global GroupSize
int 1
==
bz failed

// Save Creator
byte "A" // Admin
txn Sender
app_global_put

// by default Minter = Owner
byte "MA" // MintAccount
txn Sender
app_global_put

// GS (GlobalStatus) starts disabled

b success

//
// END: Creation of the app
//
/////////////////////////////////////////////////////////////////////

not_creation:
// is it an admin action
byte "A" // Admin
app_global_get

dup
store 4
txn Sender
==
bz no_admin

/////////////////////////////////////////////////////////////////////
// BEGIN: ADMIN SECTION
// ====================
//

// Handle each possible OnCompletion type for admin

txn OnCompletion
int DeleteApplication
==
bnz admin_handle_delete

txn OnCompletion
int UpdateApplication
==
bnz admin_handle_update

txn OnCompletion
int NoOp
==
bnz admin_handle_noop

// OnCompletion value is either OptIn, CloseOut, or ClearState
// This is forbidden as admin, as admin is the creator
err

// Handle Admin Delete
// -------------------
// Admin -> deleteApp: args []
admin_handle_delete:

global GroupSize
int 1
==
bnz success

err

// Handle Admin Update
// -------------------
// Admin -> updateApp: args []
admin_handle_update:

global GroupSize
int 1
==
bnz success

err

// Handle Admin NoOp
// -----------------
admin_handle_noop:

txna ApplicationArgs 0
byte "wAF" // withdrawAdminFees
==
bnz admin_withdraw_admin_fees

// all the rest admin operations require only 1 tx
global GroupSize
int 1
==
bz failed

txna ApplicationArgs 0
byte "sGS" // setGlobalStatus
==
bnz admin_set_global_status

txna ApplicationArgs 0
byte "sMA" // setMintAccount
==
bnz admin_set_mint_account

txna ApplicationArgs 0
byte "sAA" // setAdminAccount
==
bnz admin_set_admin_account

txna ApplicationArgs 0
byte "sAS" // setAccountStatus
==
bnz admin_set_account_status

txna ApplicationArgs 0
byte "sMF" // setMintFee
==
bnz admin_set_mint_fee

txna ApplicationArgs 0
byte "sBF" // setBurnFee
==
bnz admin_set_burn_fee

txna ApplicationArgs 0
byte "sCF" // setCreationFee
==
bnz admin_set_creation_fee

// Unknown action

err


// Handle setGlobalStatus
// -----------------
// Admin -> setGlobalStatus: args ['sGS', 0|1]
admin_set_global_status:

txna ApplicationArgs 1
btoi

dup
store 8

// GlobalStatus can be 0 or 1
int 0
==
load 8
int 1
==
||
bz failed

byte "GS" // GlobalStatus
load 8
app_global_put

b success

// End setGlobalStatus
/////////////////////////////////////////////////////////////////////


// Handle withdrawAdminFees
// -----------------
// admin -> withdrawAdminFees: args: ['waf'] accounts: [account]
admin_withdraw_admin_fees:

int 1
txn ApplicationID
byte "v" // vault
app_local_get_ex
// registered?
bz failed
store 3 // Vault address

// tx1: App withdrawAdminFee
// tx2: Payment
global GroupSize
int 2
==

// is the same Vault that corresponds to the account sending the ALGOs
load 3
gtxn 1 Sender
==
&&

// Payment
gtxn 1 TypeEnum
int 1
==
&&

// check account: tx1 Receiver == txn Sender == Admin
gtxn 1 Receiver
txn Sender
==
&&

gtxn 1 Fee
global MinTxnFee
==
&&

// no Close
gtxn 1 CloseRemainderTo 
global ZeroAddress
==
&&

bz failed

// store amount to verify
gtxn 1 Amount
//txna ApplicationArgs 1
//btoi

store 7

// substract to fees
int 1
byte "fees"
app_local_get

load 7
-
store 8

int 1
byte "fees" // fees = fees - amountWithdrew - txFee
load 8
app_local_put

b success

// End withdrawAdminFees
/////////////////////////////////////////////////////////////////////


// Handle setMintAccount
// -----------------
// Admin -> setMintAccount: args ['sMA'] accounts: [New Mint account]
admin_set_mint_account:

byte "MA" // MintAccount
txn Accounts 1
app_global_put

b success

// End setMintAccount
/////////////////////////////////////////////////////////////////////


// Handle setAdminAccount
// -----------------
// Admin -> setAdminAccount: args ['sAA'] accounts: [New Admin]
admin_set_admin_account:

byte "A" // Admin
txn Accounts 1
app_global_put

b success

// End setAdminAccount
/////////////////////////////////////////////////////////////////////



// Handle setAccountStatus
// -----------------
// Admin -> setAccountStatus: args ['s', 0|1] accounts: [account to enable/disable]
admin_set_account_status:

txna ApplicationArgs 1
btoi

dup
store 8

// status can be 0 or 1
int 0
==
load 8
int 1
==
||
bz failed

int 1
byte "s" // status
load 8
app_local_put

b success

// End setAccountStatus
/////////////////////////////////////////////////////////////////////

// Handle setMintFee
// -----------------
// Admin -> setMintFee: args ['sMF', 0-5000]
admin_set_mint_fee:

txna ApplicationArgs 1
btoi

dup
store 8

// mint fee between 0 and 5000 (0%-50%)
int 0
>=
load 8
int 5000
<=
&&
bz failed

byte "MF" // MintFee
load 8
app_global_put

int 1
return

// End setMintFee
/////////////////////////////////////////////////////////////////////


// Handle setBurnFee
// -----------------
// Admin -> setBurnFee: args ['sBF', 0-5000]
admin_set_burn_fee:

txna ApplicationArgs 1
btoi

dup
store 8

// burn fee between 0 and 5000 (0%-50%)
int 0
>=
load 8
int 5000
<=
&&
bz failed

byte "BF" // BurnFee
load 8
app_global_put

b success

// End setBurnFee
/////////////////////////////////////////////////////////////////////

// Handle setCreationFee
// -----------------
// Admin -> setCreationFee: args ['sCF', int (microALGOs)]
admin_set_creation_fee:

byte "CF" // BurnFee
txna ApplicationArgs 1
btoi
app_global_put

b success

// End setCreationFee
/////////////////////////////////////////////////////////////////////

//
// END: ADMIN SECTION
// ====================
/////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// BEGIN: ACCOUNT SECTION
// ====================
//

no_admin:

// Fail is GlobalStatus = 0
byte "GS" // GlobalStatus
app_global_get
bz failed

// OptIn
int OptIn
txn OnCompletion
==
bnz no_admin_opt_in

// Pre-requisites for account operations:
// all operations have 2 txs and account must be registered
// Registered(Sender) && GroupSize == 2 && Status(account) == enabled

int 0
txn ApplicationID
byte "v" // vault
app_local_get_ex
// registered?
bz failed

// store Vault address
store 3

global GroupSize
int 2
==

// verify if the Account status is enabled
int 0
byte "s" // status
app_local_get
&&

bz failed 

// CloseOut
int CloseOut
txn OnCompletion
==
bnz no_admin_close_out

txna ApplicationArgs 0
byte "mw" // mintwALGOs
==
bnz no_admin_mint_walgos

txna ApplicationArgs 0
byte "wA" // withdrawALGOs
==
bnz no_admin_withdraw_algos

txna ApplicationArgs 0
byte "bw" // burnwALGOs
==
bnz no_admin_burn_walgos


// unknown operation
err

// Handle OptIn
// -----------------
// account -> optIn: args [] accounts: [vaultAddr]
no_admin_opt_in:

// GroupSize == 1 && CreationFee == 0 || GroupSize == 2 && CreationFee > 0
global GroupSize
int 1
==
int 0
byte "CF"
app_global_get
dup
store 8
==
&&
bnz optin_no_creation_fee

global GroupSize
int 2
==
// creation fee != 0
load 8
int 0
>
&&

bz failed

gtxn 1 TypeEnum
int 1
==

// Receiver account == Admin
gtxn 1 Receiver 
load 4
==
&&

// amount sent == CreationFee
gtxn 1 Amount
load 8
>=
&&

bz failed

optin_no_creation_fee:
int 1
balance
int 0
==
bz failed

byte "Program"
byte base64 AiABoNejASYBIA==
concat

txn Sender
concat

//byte base64 MQcoEkgzABgiEg==
byte base64 KEgzABgiEjEgMgMSEA==
concat

sha512_256

dup
store 8

// verify that we are verifying the balance of the same account
txn Accounts 1
==
bz failed

// assign the Vault address to the account
int 0
byte "v" // vault
load 8
app_local_put

// account -> setAccountStatus: args ['s', 1|0]
int 0
byte "s" // status
int 1
app_local_put

b success

// End OptIn
/////////////////////////////////////////////////////////////////////


// Handle CloseOut
// -----------------
// account -> CloseOut: args [] accounts: [vaultAddr]
no_admin_close_out:

int 0
store 7

txna Accounts 0
store 8

// Amount should be == pending Admin fees
gtxn 1 Amount
store 5

// Position 3: Vault address
// Position 4: Admin address
// Position 5: tx1.Amount to cover pending Admin fees
// Position 7: Index on the Accounts array of the account to close

close_out_account:


// tx1: App CloseOut
// tx2: Payment

// tx0.TypeEnum == AppCall && tx0.TypeEnum == Payment && tx1.Sender == VaultAddr && GroupSize == 2 && tx1.Fee == MinTxnFee && tx1.Receiver == Admin && Minted == 0 && tx1.To == pendingFees
// Payment
gtxn 1 TypeEnum
int 1
==

// is the same Vault that corresponds to the account sending the ALGOs
load 3
gtxn 1 Sender
==
&&

gtxn 1 Fee
global MinTxnFee
==
&&

// Receiver account == Admin
gtxn 1 Receiver 
load 4
==
&&

// user should burn the algos before CloseOut
load 7
byte "m" // minted
app_local_get

int 0
==
&&

load 7
byte "fees" // fees owed
app_local_get

// Receiver (Admin) amount == pending Admin fees
load 5
==
&&
bnz success

err

// End CloseOut
/////////////////////////////////////////////////////////////////////


// Handle mintwALGOs
// -----------------
// account -> mintwALGOs: args ['mw'] accounts: [vaultAddr]
no_admin_mint_walgos:

// is the same Vault that corresponds to the account
// tx0.Accounts[1] == VaultAddr && tx1.TypeEnum == AssetTransfer && tx1.AssetAmount > 0 && 
// tx1.Sender == MintAccount && tx1.Fee == MinTxnFee && tx1.AssetCloseTo == ZeroAddress && 
// tx1.XferAsset == ASA_ID
load 3
txn Accounts 1
==

// tx1: App mintwALGOs
// tx2: ASA transfer
// AssetTransfer
gtxn 1 TypeEnum
int 4
==
&&

// more than 0
gtxn 1 AssetAmount
int 0
>
&&

gtxn 1 Sender
byte "MA" // MintAccount
app_global_get
==
&&

gtxn 1 Fee
global MinTxnFee
==
&&

// no Close
gtxn 1 AssetCloseTo 
global ZeroAddress
==
&&

// ASA_ID
gtxn 1 XferAsset
int 2671688
==
&&

bz failed

// Minted = Minted + tx1.AssetAmount
int 0
byte "m" // minted
app_local_get

//txna ApplicationArgs 1
//btoi
gtxn 1 AssetAmount
dup
dup
// amount to tax
store 8
// amount minting to verify 
store 7
+
store 6

int 0
byte "m" // minted
load 6
app_local_put

// add the fee to owner account
byte "MF"
app_global_get

store 9
int 1 
store 6

int 0
store 7

// Position 6: verify balance == 1 (true)
// Position 7: 0 because "m" has the amount to mint included 
// Position 8: mint amount, amount to tax
// Position 9: mint fee, tax
b charge_fees

// End mintwALGOs: Go to charge_fees
/////////////////////////////////////////////////////////////////////



// Handle withdrawALGOs
// -----------------
// account -> withdrawALGOs: args: ['wA'] accounts: [vaultAddr]
no_admin_withdraw_algos:

// tx1: App withdrawALGOs
// tx2: Payment

// tx0.Accounts[1] == VaultAddr && tx1.Sender == VaultAddr && tx1.TypeEnum == Payment && 
// tx1.Amount >= 1000 && tx1.Fee == MinTxnFee && tx1.CloseRemainderTo == ZeroAddress
load 3 // vault address
txna Accounts 1
==

// is the same Vault that corresponds to the account sending the ALGOs
load 3
gtxn 1 Sender
==
&&

// Payment
gtxn 1 TypeEnum
int 1
==
&&

// check amounts
gtxn 1 Amount
store 7

// more than 1000 (Minimum Withdrawal)
load 7
int 1000
>=
&&

gtxn 1 Fee
global MinTxnFee
==
&&

// no Close
gtxn 1 CloseRemainderTo 
global ZeroAddress
==
&&

bz failed

// the total amount to validate is withdrawal amount + txFee
load 7
dup
store 8
gtxn 1 Fee
//int 1000
+
store 7

int 1
store 6

// Position 6: verify balance == 1
// Position 7: verify amount to withdraw/mint (Mint/Withdraw) or 0 if it is not necessary (Burn/Deposit)

b verify_balance

// End withdrawALGOs: Go to verify_balance
/////////////////////////////////////////////////////////////////////


no_admin_burn_walgos:


// account -> burnwALGOs: args: ['bw']

// tx1: App BurnwALGOs
// tx2: ASA transfer to Minter
// ApplicationCall
//txn 0 TypeEnum
//int 6
//==
// AssetTransfer
gtxn 1 TypeEnum
int 4
==
//&&

// more than 0
gtxn 1 AssetAmount
int 0
>
&&

// Minter == ASA Receiver
gtxn 1 AssetReceiver
byte "MA" // MintAccount
app_global_get
==
&&

// ASA ID
gtxn 1 XferAsset
int 2671688
==
&&

bz failed

int 0
byte "m" // minted
app_local_get

dup
store 8

// amount to burn less or equal to amount minted. If it is not verified the next substraction generates an error if the 
// the amount burned is greater than the minted. 
gtxn 1 AssetAmount
//txna ApplicationArgs 1
//btoi
>=
bz failed

load 8
gtxn 1 AssetAmount
//txna ApplicationArgs 1
//btoi
-
store 8

int 0
byte "m" // minted
load 8
app_local_put

// add the fee to owner account
byte "BF"
app_global_get

store 9
gtxn 1 AssetAmount
//txna ApplicationArgs 1
//btoi
store 8
int 0
store 6

// End burnwALGOs: Go to charge_fees
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
//
// BEGIN: charge_fees
// ==========================
// Position 6: 0 Do not verify balances, 1 verify balances
// Position 7: balance to verify if Position 6 != 0
// Position 8: mint/burn amount, amount to tax
// Position 9: fee to tax
//

charge_fees:

load 8
load 9
*
int 10000
/
// Position 8: fees to add
store 8

int 0
byte "fees" // fees = fees + newFees
app_local_get

// tax fee
load 8
+
store 8

int 0
byte "fees"
load 8
app_local_put

load 6
int 0
==
bnz success

// End charge_fees: Go to verify_balance
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// BEGIN: verify_balance
// ==========================
// Position 6: use this amount as minted
// Position 7: amount to Withdraw/Burn. It must be less than the balance - AdminFees. Withdraw amount must include tx fees
//

verify_balance:

// Balance - withdraw/mint amount - fees >= amount minted
int 0
byte "fees" 
app_local_get
dup
store 8
int 0
>
bz verify_balance_no_fees

// keep a tx fee to withdraw/close
load 8
global MinTxnFee
+
store 8

verify_balance_no_fees:

// Balance - WithdrawalAmount - AdminFees >= Minted
// Keep also MinTxFee in the account to withdraw/close it

// Vault Balance
int 1
balance

// Withdraw amount
load 7
-

// admin fees
load 8
-

int 0
byte "m" // minted
app_local_get

>=
bz failed

b success

// End verify_balance
/////////////////////////////////////////////////////////////////////

//
// END: ACCOUNT SECTION
// ====================
/////////////////////////////////////////////////////////////////////

failed:
int 0
return

success:
int 1
return